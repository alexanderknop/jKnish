class Rational {
    construct fraction(a, b) {
        var sign = a.sign * b.sign;
        _a = sign * a.abs / Rational.gcd(a, b);
        _b = b.abs / Rational.gcd(a, b);
    }

    construct integer(a) {
        _a = a;
        _b = 1;
    }

    numerator {
        return _a;
    }

    denominator {
        return _b;
    }

    *(right) {
        return Rational.fraction(
            this.numerator * right.numerator,
            this.denominator * right.denominator
        );
    }

    /(right) {
        return Rational.fraction(
            this.numerator * right.denominator,
            this.denominator * right.numerator
        );
    }

    +(right) {
        return Rational.fraction(
            this.numerator * right.denominator +
                right.numerator * this.denominator,
            this.denominator * right.denominator
        );
    }

    -(right) {
        return Rational.fraction(
            this.numerator * right.denominator -
                right.numerator * this.denominator,
            this.denominator * right.denominator
        );
    }

    - {
        return Rational.fraction(
            -this.numerator,
            this.denominator
        );
    }

    ==(right) {
        return this.numerator == right.numerator and
            this.denominator == right.denominator;
    }

    toString {
        if (this.denominator != 1) {
            return this.numerator.toString + " / " + this.denominator.toString;
        } else {
            return this.numerator.toString;
        }
    }

    static gcd(a, b) {
        a = a.abs;
        b = b.abs;

        if (a == 0) {
            return b;
        }

        if (b == 0) {
            return a;
        }

        if (a > b) {
            return Rational.gcd(b, a % b);
        } else {
            return Rational.gcd(a, b % a);
        }
    }
}

// expect: 2
System.print(Rational.gcd(2, 4));
var q = Rational.fraction(2, 4);
// expect: 1 / 4
System.print(q * q);
// expect: 1
System.print(q + q);
// expect: 0
System.print(q - q);
// expect: -1 / 2
System.print(-q);
// expect: 2
System.print(Rational.integer(1) / q);
// expect: true
System.print(q == q);
// expect: false
System.print(Rational.integer(1) == q);
// expect: -1
System.print(Rational.fraction(1, -1));